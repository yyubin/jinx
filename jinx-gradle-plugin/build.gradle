plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'
    id 'com.gradle.plugin-publish' version '2.0.0'
    id("io.github.sgtsilvio.gradle.maven-central-publishing") version "0.4.1"
}

dependencies {
    implementation project(':jinx-core')
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.15.2'

    compileOnly "org.projectlombok:lombok:${versions.lombok}"
    annotationProcessor "org.projectlombok:lombok:${versions.lombok}"

    testImplementation platform("org.junit:junit-bom:${versions.junit}")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation("org.mockito:mockito-core:${versions.mockito}")
    testImplementation("org.assertj:assertj-core:${versions.assertj}")
    testImplementation gradleTestKit()
}

gradlePlugin {
    website = 'https://github.com/yyubin/jinx'
    vcsUrl = 'https://github.com/yyubin/jinx.git'

    plugins {
        jinx {
            id = 'io.github.yyubin.jinx'
            implementationClass = 'org.jinx.gradle.JinxPlugin'
            displayName = 'Jinx Gradle Plugin'
            description = 'Gradle plugin for Jinx JPA DDL generation tool'
            tags.set(['jpa', 'ddl', 'schema', 'generator', 'jinx'])
        }
    }
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

afterEvaluate {

    publishing {
        publications {
            named('pluginMaven', MavenPublication) {
                artifactId = 'jinx-gradle-plugin'

                pom {
                    name = 'jinx-gradle-plugin'
                    description = 'Gradle plugin for Jinx JPA DDL generation tool'
                    url = 'https://github.com/yyubin/jinx'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-20.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'yyubin'
                            name = 'Yubin'
                            email = 'yyubin@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/yyubin/jinx.git'
                        developerConnection = 'scm:git:ssh://github.com/yyubin/jinx.git'
                        url = 'https://github.com/yyubin/jinx'
                    }
                }
            }

            // Plugin Marker: Plugin Portal 게시용, Maven Central에서는 의미 없음
            all { pub ->
                if (pub.name.contains("PluginMarkerMaven")) {
                    pub.pom {
                        name = 'Jinx Gradle Plugin Marker'
                        description = 'Gradle plugin marker for Jinx JPA DDL generation tool'
                        url = 'https://github.com/yyubin/jinx'

                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-20.0.txt'
                            }
                        }

                        developers {
                            developer {
                                id = 'yyubin'
                                name = 'Yubin'
                                email = 'yyubin@gmail.com'
                            }
                        }

                        scm {
                            connection = 'scm:git:git://github.com/yyubin/jinx.git'
                            developerConnection = 'scm:git:ssh://github.com/yyubin/jinx.git'
                            url = 'https://github.com/yyubin/jinx'
                        }
                    }
                }
            }
        }
    }
}

signing {
    setRequired(false)

    if (project.hasProperty("signingKey")) {
        useInMemoryPgpKeys(
                project.findProperty("signingKey"),
                project.findProperty("signingPassword")
        )

        sign(publishing.publications.named("pluginMaven"))
    }
}

