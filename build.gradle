plugins {
    id 'java'
    id 'signing'
    id("io.github.sgtsilvio.gradle.maven-central-publishing") version "0.4.1"
    id("com.github.spotbugs") version "6.4.2"
    id "org.sonarqube" version "6.3.1.5724"
}

group = 'io.github.yyubin'
version = '0.0.19'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
        // Remove vendor restriction for better compatibility
    }
}

allprojects {
    repositories { mavenCentral() }

    group = rootProject.group
    version = rootProject.version

    // Force Java 21 toolchain for all subprojects
    plugins.withType(JavaPlugin) {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(21)
                // Remove vendor restriction for better compatibility
            }
        }
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

ext {
    versions = [
            junit        : '5.10.0',
            jackson      : '2.15.2',
            lombok       : '1.18.32',
            picocli      : '4.7.5',
            jpaApi       : '3.2.0',
            autoService  : '1.1.1',
            mockito      : '5.4.0',
            assertj      : '3.27.3',
    ]
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'

    dependencies {
        compileOnly "org.projectlombok:lombok:${versions.lombok}"
        annotationProcessor "org.projectlombok:lombok:${versions.lombok}"

        testImplementation platform("org.junit:junit-bom:${versions.junit}")
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation("org.junit.jupiter:junit-jupiter-api:5.10.2")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.10.2")
        testImplementation "org.mockito:mockito-core:${versions.mockito}"
        testImplementation("org.mockito:mockito-junit-jupiter:5.11.0")
        testImplementation("org.assertj:assertj-core:${versions.assertj}")
    }

    test {
        useJUnitPlatform()
        finalizedBy 'jacocoTestReport'
    }

    jacoco {
        toolVersion = "0.8.12"
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.required = true
        }
        afterEvaluate {
            classDirectories.setFrom(
                    files(classDirectories.files.collect {
                        fileTree(dir: it, exclude: [
                                '**/model/**'
                        ])
                    })
            )
        }
    }
}
